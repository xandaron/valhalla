#version 450

layout (binding = 0, rgba8) uniform readonly image2D inImage;
layout (binding = 1, rgba8) uniform writeonly image2D outImage;

// x * y * z <= 1024; must be true
layout (local_size_x = 32, local_size_y = 32, local_size_z = 1) in;

int radius = 8;

float hardness = 17.0;
float sharpness = 10.0;

float zeta = 0.1;
float zeroCross = 0.4;
float sinZeroCross = sin(zeroCross);
float eta = (zeta + cos(zeroCross)) / (sinZeroCross * sinZeroCross);

void main() {
    vec4[8] m;
    vec3[8] s;

    for (int i = 0; i < 8; i++) {
        m[i] = vec4(0.0, 0.0, 0.0, 0.0);
        s[i] = vec3(0.0, 0.0, 0.0);
    }

    ivec2 uv = ivec2(gl_GlobalInvocationID.xy);

    for (int x = -radius; x <= radius; x++) {
        for (int y = -radius; y <= radius; y++) {
            vec2 v = vec2(x, y) / radius;
            vec3 colour = imageLoad(inImage, uv + ivec2(x, y)).rgb;

            float[8] weights;
            float sum = 0.0;

            // Polynomial weights
            float vxx = zeta - eta * v.x * v.x;
            float vyy = zeta - eta * v.y * v.y;

            float z = max(0, v.y + vxx);
            weights[0] = z * z;
            sum += weights[0];

            z = max(0, -v.x + vyy);
            weights[2] = z * z;
            sum += weights[2];

            z = max(0, -v.y + vxx); 
            weights[4] = z * z;
            sum += weights[4];

            z = max(0, v.x + vyy); 
            weights[6] = z * z;
            sum += weights[6];

            v = (sqrt(2.0) / 2.0) * vec2(v.x - v.y, v.x + v.y);
            vxx = zeta - eta * v.x * v.x;
            vyy = zeta - eta * v.y * v.y;

            z = max(0, v.y + vxx); 
            weights[1] = z * z;
            sum += weights[1];

            z = max(0, -v.x + vyy); 
            weights[3] = z * z;
            sum += weights[3];

            z = max(0, -v.y + vxx); 
            weights[5] = z * z;
            sum += weights[5];

            z = max(0, v.x + vyy); 
            weights[7] = z * z;
            sum += weights[7];

            float g = exp(-3.125 * dot(v, v)) / sum;
            for (int k = 0; k < 8; k++) {
                float wk = weights[k] * g;
                m[k] += vec4(colour * wk, wk);
                s[k] += colour * colour * wk;
            }
        }
    }

    vec4 outColour = vec4(0.0, 0.0, 0.0, 0.0);
    for (int k = 0; k < 8; k++) {
        m[k].rgb /= m[k].w;
        s[k] = abs(s[k] / m[k].w - m[k].rgb * m[k].rgb);

        float sigma2 = s[k].r + s[k].g + s[k].b;
        float w = 1.0 / (1.0 + pow(hardness * 1000.0 * sigma2, 0.5 * sharpness));

        outColour += vec4(m[k].rgb * w, w);
    }

    imageStore(outImage, uv, vec4(outColour.xyz / outColour.w, 1.0));
}